def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.zsl.flutterdemo"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        //只加载简体中文
        resConfigs "zh-rCN"
        //ndk设置
        ndk {
            //只要arm64-v8a的动态库
            abiFilters "arm64-v8a","x86"
        }
        //测试框架
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // Signing with the debug keys for now, so `flutter run --release` works.
            //启用资源压缩
            shrinkResources true
            //启用代码压缩
            minifyEnabled true
            //签名
            signingConfig signingConfigs.debug
        }
        debug {
            //版本名称增加后缀
            versionNameSuffix "_debug"
            //签名
            signingConfig signingConfigs.debug
        }
    }

    //制定kotlin 代码目录
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    //lint检查项
    lintOptions {
        //禁用无效包名检查
        disable 'InvalidPackage'
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
